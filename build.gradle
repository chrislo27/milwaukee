import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.tasks.KotlinCompilationTask

buildscript {
    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.github.johnrengelman:shadow:8.1.1"
    }
}

allprojects {
    apply plugin: "eclipse"
    apply plugin: "idea"
    apply plugin: "java"
    apply plugin: "kotlin"

    version = "0.1.0"
    ext {
        appName = "milwaukee"
        
        paintboxVersion = "0.16.1"
        useLocalPaintbox = false
    }
    
    java {
        toolchain.languageVersion.set(JavaLanguageVersion.of(17))
        withSourcesJar()
        withJavadocJar()
    }

    repositories {
        mavenCentral()
        mavenLocal()
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
        maven { url "https://jitpack.io/" }
    }
    
    dependencies {
    }

    tasks.withType(KotlinCompilationTask).configureEach {
        compilerOptions {
            jvmTarget.set(JvmTarget.JVM_17)
            freeCompilerArgs.add("-opt-in=kotlin.ExperimentalUnsignedTypes")
        }
    }
}

project(":core") {
    dependencies {
        if (useLocalPaintbox) {
            implementation "com.github.chrislo27:paintbox-core:${paintboxVersion}"
        } else {
            implementation "com.github.chrislo27.paintbox:paintbox-core:${paintboxVersion}"
        }

        implementation "com.badlogicgames.jlayer:jlayer:1.0.1-gdx"
        implementation "com.eclipsesource.minimal-json:minimal-json:0.9.5"
        implementation "space.earlygrey:shapedrawer:v2.6.0"
    }
}

project(":desktop") {
    dependencies {
        implementation project(":core")

        if (useLocalPaintbox) {
            implementation "com.github.chrislo27:paintbox-desktop:${paintboxVersion}"
        } else {
            implementation "com.github.chrislo27.paintbox:paintbox-desktop:${paintboxVersion}"
        }
        
        implementation "com.badlogicgames.gdx:gdx-lwjgl3-glfw-awt-macos:${libgdx_version}"
    }
}